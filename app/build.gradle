apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        applicationId "me.liaoheng.wallpaper"
        versionCode 135
        versionName "1.3.5"
    }

    def isSigning = true
    signingConfigs {
        config {
            try {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file(STORE_FILE)
                storePassword STORE_PASSWORD
            } catch (Exception ignored) {
                isSigning = false
                printf("not signingConfigs")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isSigning) signingConfig signingConfigs.config
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}.apk"
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    testImplementation deps.robolectric
    testImplementation deps.junit
    testImplementation 'joda-time:joda-time:2.10.1'
    //android system
    implementation deps.appcompat
    implementation deps.cardview
    implementation deps.design
    implementation deps.palette
    implementation deps.customtabs

    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    //    implementation 'com.google.android.gms:play-services-base:15.0.1'

    implementation 'com.jakewharton:butterknife:9.0.0-rc2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc2'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation deps.glide
    annotationProcessor deps.glide_compiler
    implementation deps.glide_okhttp3_integration
    implementation deps.okhttp
    implementation deps.retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation deps.retrofit_adapter_rxjava2
    implementation deps.rxandroid2
    implementation deps.rxjava2
    implementation deps.rxlifecycle3
    implementation deps.rxlifecycle_android3
    implementation deps.joda_time_android

    //https://github.com/ferrannp/material-preferences
    implementation 'com.fnp:material-preferences:0.1.4'

    //https://github.com/yshrsmz/LicenseAdapter
    implementation 'net.yslibrary.licenseadapter:licenseadapter:2.2.2'

    //https://github.com/H07000223/FlycoSystemBar
    implementation "com.flyco.systembar:FlycoSystemBar_Lib:1.0.0"

    //https://github.com/Clans/FloatingActionButton
    implementation 'com.github.clans:fab:1.6.4'

    //https://github.com/grandcentrix/tray
    implementation 'net.grandcentrix.tray:tray:0.12.0'

    //https://github.com/apl-devs/AppIntro
    implementation 'com.github.paolorotolo:appintro:v5.1.0'

//    implementation group: "com.github.liaoheng", name: "common-core", version: "0.1.0-SNAPSHOT", changing: true
//    implementation group: "com.github.liaoheng", name: "common-adapter", version: "0.1.0-SNAPSHOT", changing: true
    implementation 'com.github.liaoheng:common-core:0.1.0-SNAPSHOT'
    implementation 'com.github.liaoheng:common-adapter:0.1.0-SNAPSHOT'
}
//configurations.all {
//    // check for updates every build
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}
apply plugin: 'com.google.gms.google-services'