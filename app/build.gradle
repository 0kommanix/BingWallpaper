apply plugin: 'com.android.application'

def isFirebase = rootProject.file('app/google-services.json').exists()
if (isFirebase) {
    apply plugin: 'com.google.gms.google-services'
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion 31
        applicationId "me.liaoheng.wallpaper"
        versionCode 10938
        versionName "1.9.38"
    }

    def isSigning = true
    signingConfigs {
        config {
            try {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file(STORE_FILE)
                storePassword STORE_PASSWORD
            } catch (Exception ignored) {
                isSigning = false
                printf("not signingConfigs")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isSigning) signingConfig signingConfigs.config
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    android.applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.all {
                outputFileName = "${variant.name}-${variant.versionName}.apk"
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

def firebaseCore = "com.google.firebase:firebase-analytics:20.0.2"

dependencies {
    testImplementation deps.robolectric
    testImplementation deps.junit
    testImplementation 'joda-time:joda-time:2.10.7'
    //android system
    implementation deps.annotations
    implementation deps.material
    implementation deps.palette
    implementation deps.browser
    implementation deps.preference

    implementation 'androidx.work:work-runtime:2.7.1'
    if (isFirebase) {
        implementation(firebaseCore) {
            exclude group: "com.google.android.gms", module: "play-services-ads-identifier"
        }
    } else {
        printf("not exists google-services.json")
        compileOnly firebaseCore
    }

    implementation 'io.sentry:sentry-android-core:5.4.1'

    implementation deps.gson
    implementation deps.glide
    annotationProcessor deps.glide_compiler
    implementation deps.glide_okhttp3_integration
    implementation deps.okhttp
    implementation deps.okhttp_logging
    implementation deps.okhttp_doh
    implementation deps.retrofit
    implementation deps.retrofit_converter_gson
    implementation deps.retrofit_adapter_rxjava2
    implementation deps.rxandroid2
    implementation deps.rxjava2
    implementation deps.rxlifecycle3
    implementation deps.rxlifecycle_android3
    implementation deps.joda_time_android

    //https://github.com/Commit451/NativeStackBlur
    implementation 'com.github.Commit451:NativeStackBlur:1.0.5'

    //https://github.com/scottyab/rootbeer
    implementation 'com.github.scottyab:rootbeer:0.1.0'

    //https://github.com/Clans/FloatingActionButton
    implementation 'com.github.clans:fab:1.6.4'

    //https://github.com/grandcentrix/tray
    implementation 'net.grandcentrix.tray:tray:0.12.0'

    //https://github.com/apl-devs/AppIntro
    implementation 'com.github.AppIntro:AppIntro:6.1.0'

    //https://github.com/davemorrissey/subsampling-scale-image-view
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'

    //    implementation group: "com.github.liaoheng", name: "common-core", version: "0.1.1-SNAPSHOT", changing: true
    //    implementation group: "com.github.liaoheng", name: "common-adapter", version: "0.1.1-SNAPSHOT", changing: true
    implementation 'com.github.liaoheng:common-core:0.1.1-SNAPSHOT'
    implementation 'com.github.liaoheng:common-adapter:0.1.1-SNAPSHOT'
}
//configurations.all {
//    // check for updates every build
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}